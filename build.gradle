plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '7.2.1'
    id 'org.asciidoctor.jvm.convert' version '4.0.5'
}

group = 'com.irum'
version = '0.0.1-SNAPSHOT'
description = 'productservice'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/Profect-4th-IRUM/open-feign-client"
        credentials {

            username = System.getenv("GIT_USERNAME") ?: project.findProperty("gpr.user")
            password = System.getenv("GIT_TOKEN") ?: project.findProperty("gpr.key")
        }
    }
    maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/Profect-4th-IRUM/global-module"
        credentials {

            username = System.getenv("GIT_USERNAME") ?: project.findProperty("gpr.user")
            password = System.getenv("GIT_TOKEN") ?: project.findProperty("gpr.key")
        }
    }

}

def querydslDir = layout.buildDirectory.dir("generated/querydsl")
def snippetsDir = layout.buildDirectory.dir("generated-snippets")

sourceSets {
    main {
        java {
            srcDirs += [querydslDir]
        }
    }
}

tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(querydslDir)
}

clean {
    delete querydslDir
}

ext {
    set('springCloudVersion', "2025.0.0")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // JPA & DB
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'com.h2database:h2'

    //Spring Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'


    //redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'io.github.openfeign:feign-gson:11.0'
    implementation 'io.github.openfeign:feign-okhttp'

    // Config Server
    implementation 'org.springframework.cloud:spring-cloud-starter-config'

    // Service-Discovery
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    // common module
    implementation 'com.irum:open-feign-client:1.0.0-SNAPSHOT'
    implementation 'com.irum:global-module:0.1.8-SNAPSHOT'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // 낙관적 락 재시도
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.retry:spring-retry'

    // Embedded Redis
    testImplementation('it.ozimov:embedded-redis:0.7.3') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
}

test {
    useJUnitPlatform()
    outputs.dir snippetsDir
}

asciidoctor.doFirst {
    delete layout.projectDirectory.dir("src/main/resources/static/docs")
}

asciidoctor {
    baseDirFollowsSourceFile()            // include 기준을 index.adoc 위치로 고정
    sources { include 'index.adoc' }      // index.adoc을 진입점으로
    attributes('snippets': snippetsDir.get().asFile)
    configurations 'asciidoctorExt'
    dependsOn test
    baseDirFollowsSourceDir()
}

bootJar {
    if (project.hasProperty('docs')) {
        dependsOn asciidoctor
    }
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

spotless {
    java {
        targetExclude("**/generated/querydsl/**")
        // Google Java 포맷 적용
        googleJavaFormat().aosp()
        // import 순서 정렬
        importOrder()
        // 사용하지 않는 import 제거
        removeUnusedImports()
        // 각 라인 끝에 있는 공백을 제거
        trimTrailingWhitespace()
        // 파일 끝에 새로운 라인 추가
        endWithNewline()
    }
}

tasks.register('updateGitHooks', Copy) {
    from('./scripts/') { include 'pre-commit' }
    into './.git/hooks'
}

tasks.register('makeGitHooksExecutable', Exec) {
    commandLine 'chmod', '+x', './.git/hooks/pre-commit'
    dependsOn updateGitHooks
}
